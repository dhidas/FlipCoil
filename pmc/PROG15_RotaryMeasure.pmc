#include "PVariables.pmc"
#include "MVariables.pmc"
#include "Constants.pmc"


; Used internally
#define Q_RotaryStart       Q400 ; Actual rotary start position
#define Q_RotaryStop        Q401 ; Actual rotary stop position
#define Q_RotaryTrigWidth   Q402 ; Trigger width in degrees
#define Q_RotaryDirection   Q403 ; Direction of scan based off R0 and RF
#define Q_RotaryDOver       Q404 ; Distance for ramp (Purposly not accounting for accel, in clear)
#define Q_RotaryDelta       Q405 ; Distance between triggers
#define Q_RotaryNOver       Q406 ; Number of extra triggers before R0
#define Q_RotaryFirst       Q407 ; Position of first actual trigger
#define Q_RotaryBehind      Q408 ; Position of trigger up edge behind starting point

Q410 = 0

Open PROG 15 Clear

LINEAR
ABS
TA(P_RotaryTOver)
TS(P_RotaryTOver / 2)

FRAX(A)
F(kROTARYQUICKSPEED)


; Get the trigger compare out of the way
M_PosComp7Inc = 0
M_PosComp7A = 100 * 360 / P_DegPerStep
M_PosComp7B = 100 * 360 / P_DegPerStep

; Possible errors
P_RotaryNotHomed = 0
P_RotaryWrongTriggerState = 0

; Initial calculations
Q_RotaryDirection = +1
if (P_RotaryR0 > P_RotaryRF)
  Q_RotaryDirection = -1
endif
Q_RotaryDOver   = P_RotaryTOver * P_DegPerSecond
Q_RotaryDelta   = ABS((P_RotaryRF - P_RotaryR0) / (P_RotaryNTriggers - 1))
Q_RotaryNOver   = INT(Q_RotaryDOver / Q_RotaryDelta)
Q_RotaryTrigWidth = Q_RotaryDelta / 2

if (ABS(P_RotaryTOver * P_DegPerSecond - Q_RotaryDelta * Q_RotaryNOver) < Q_RotaryTrigWidth / 10)
  Q_RotaryDOver = Q_RotaryDOver + Q_RotaryTrigWidth / 5
  Q410 = Q_RotaryTrigWidth / 5
endif
Q_RotaryDelta   = ABS((P_RotaryRF - P_RotaryR0) / (P_RotaryNTriggers - 1))
Q_RotaryNOver   = INT(Q_RotaryDOver / Q_RotaryDelta)

Q_RotaryFirst   = P_RotaryR0 - Q_RotaryDirection * Q_RotaryNOver * Q_RotaryDelta
Q_RotaryBehind  = Q_RotaryFirst + Q_RotaryDirection * (Q_RotaryTrigWidth - Q_RotaryDelta)
P_RotaryNTotalTriggers = P_RotaryNTriggers + 2 * Q_RotaryNOver

; Calculate actual start and stop positions for motor movement
Q_RotaryStart = P_RotaryR0 - Q_RotaryDirection * Q_RotaryDOver
Q_RotaryStop  = P_RotaryRF + Q_RotaryDirection * Q_RotaryDOver


; Check if motors homed or not
if (M_HomeComplete_M7 != 1 or M_HomeComplete_M8 != 1)
  P_RotaryNotHomed = 1
  return
endif

if (M_PosComp7Val != 1)
  P_RotaryWrongTriggerState = 1
  return
endif

; Move to starting position
A(Q_RotaryStart) B(P_RotaryOffset)
Dwell 10
F(P_DegPerSecond)

; First trigger must be at:
M_PosComp7Inc = Q_RotaryDelta / P_DegPerStep
M_PosComp7B = Q_RotaryFirst / P_DegPerStep
M_PosComp7A = Q_RotaryBehind / P_DegPerStep
gTRIG7HIGH

Dwell 100

; Move to stopping position
A(Q_RotaryStop) B(P_RotaryOffset)
Dwell 100
gTRIG7HIGH


Close
